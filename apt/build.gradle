plugins {
    id 'java-library'
    id 'maven-publish'
}
var $groupId = 'io.github.yeamy'
var $artifactId = 'restlite-apt'
var $version = '1.0-RC7'

group $groupId
version $version

repositories {
    mavenCentral()
}

dependencies {
    api project(':core')
//    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
//    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}
//
//test {
//    useJUnitPlatform()
//}

tasks.register('generateSourceJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc {// create document
    source = sourceSets.main.java.srcDirs
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "https://docs.oracle.com/en/java/javase/17/docs/api"
    }
}

tasks.register('generateJavadoc', Jar) { // package document as jar
    archiveClassifier.set('javadoc')
    from "$rootDir/javadoc"
}

tasks.register("closeUpload") {
    doLast {
        var uri = new URL("https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/" + $groupId)
        var key = ossrhUsername + ':' + ossrhPassword
        var auth = "Basic " + Base64.encoder.encodeToString(key.bytes)
        var conn
        try {
            conn = uri.openConnection() as HttpURLConnection
            conn.requestMethod = "POST"
            conn.setRequestProperty("Authorization", auth)
            println("HTTP response code: " + conn.responseCode)
        } catch (Exception e) {
            println("HTTP request exception: ${e.message}")
            e.printStackTrace()
        } finally {
            if (conn != null) conn.disconnect()
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            groupId $groupId
            artifactId $artifactId
            version $version
            pom {
                name = $artifactId
                description = 'An apt framework for RESTlite, make it easy to create services.'
                url = 'https://github.com/Yeamy/RESTlite'
                inceptionYear = '2023'
                scm {
                    url = 'https://github.com/Yeamy/RESTlite'
                    connection = 'scm:git@https://github.com/Yeamy/RESTlite.git'
                    developerConnection = 'scm:git@https://github.com/Yeamy/RESTlite.git'
                }
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org/'
                        distribution = 'repo'
                        comments = 'A business-friendly OSS license'
                    }
                }
                developers {
                    developer {
                        id = 'yeamy0754'
                        name = 'Yeaming'
                        email = 'yeamy0754@hotmail.com'
                        url = 'https://github.com/Yeamy'
                    }
                }
                issueManagement {
                    system = 'Github'
                    url = 'https://github.com/Yeamy/RESTlite'
                }
            }
            artifact generateSourceJar
            artifact generateJavadoc
        }
    }

    repositories {
        maven {
            name = 'ossrh-staging-api'
            url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

apply plugin: 'signing'
signing {
    sign publishing.publications.maven
}
